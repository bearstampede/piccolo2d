/*
 * Copyright (c) 2008, Piccolo2D project, http://piccolo2d.org
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without modification, are permitted provided
 * that the following conditions are met:
 *
 * Redistributions of source code must retain the above copyright notice, this list of conditions
 * and the following disclaimer.
 *
 * Redistributions in binary form must reproduce the above copyright notice, this list of conditions
 * and the following disclaimer in the documentation and/or other materials provided with the
 * distribution.
 *
 * None of the name of the Piccolo2D project, the University of Maryland, or the names of its contributors
 * may be used to endorse or promote products derived from this software without specific prior written
 * permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED
 * WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
 * PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR
 * ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
 * TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
 * ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */
package org.piccolo2d.svg;

import java.awt.geom.GeneralPath;
import java.text.ParseException;

/** <a href="http://research.cs.queensu.ca/~thurston/ragel/">Ragel> parser 
 * for <a href="http://www.w3.org/TR/SVG11/shapes.html#PointsBNF">list of points</a> - This file is auto-generated by rl2java.sh.
 * <p>
 * DO NOT EDIT MANUALLY!!!
 * </p>
 */
class PointParser {
%%{

	machine points;

	#######################################################
	## Define the actions
	#######################################################

	action CLEAR {
		argc = 0;
	}

	action BUF {
		buf.append(data[p]);
	}

	action NUM {
		argv[argc++] = Float.parseFloat(buf.toString());
		buf.setLength(0);
	}

	action MOVETO {
		gp.moveTo(argv[0], argv[1]);
		argc = 0;
	}

	action LINETO {
		gp.lineTo(argv[0], argv[1]);
		argc = 0;
	}

	#######################################################
	## Define the grammar
	#######################################################

	wsp = space;
	digit_sequence = digit+;
	sign = "+" | "-";
	exponent = ( "e" | "E" ) sign? digit_sequence;
	fractional_constant = (digit_sequence? "." digit_sequence) | (digit_sequence ".");
	floating_point_constant = (fractional_constant exponent?) | (digit_sequence exponent);
	integer_constant = digit_sequence;
	comma = ",";
	comma_wsp = (wsp+ comma? wsp*) | (comma wsp*);
	number = (sign? @BUF (integer_constant | floating_point_constant)) @BUF %NUM;

	coordinate = number;
	coordinate_pair = coordinate comma_wsp coordinate;
	coordinate_pairs = coordinate_pair >CLEAR %MOVETO ( comma_wsp coordinate_pair >CLEAR %LINETO )*;
	
	main := wsp* coordinate_pairs? wsp*;
}%%

%% write data;

	final GeneralPath parse(final CharSequence data) throws ParseException {
		return parse(data == null ? null : data.toString().toCharArray());
	}

	final GeneralPath parse(final char[] data) throws ParseException {
		final GeneralPath gp = new GeneralPath();
		if(data == null)
			return gp;
		// high-level buffers
		final StringBuilder buf = new StringBuilder();
		final float[] argv = new float[2];
		int argc = 0;
		
		// ragel variables (low level)
		final int pe = data.length;
		final int eof = pe;
		int cs, p = 0;
		int top;

		%% write init;
		%% write exec;

		if ( cs < points_first_final )
			throw new ParseException(new String(data), p);
        return gp;
	}
}
